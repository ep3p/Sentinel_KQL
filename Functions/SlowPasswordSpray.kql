// This query can help you to detect slow password spray events.
// If you want to check all the activity (not the new activity) over a time period, make sure query_frequency and query_period parameters are the same value.
//
// Click "Save as function", in Parameters write in the fields:
// "timespan" "query_frequency" "14d"
// "timespan" "query_period"    "14d"
//
// If you name the function "SlowPasswordSpray", you can check the function with queries like the following:
//
// SlowPasswordSpray()
//
// SlowPasswordSpray(1h, 14d)
//
// SlowPasswordSpray(14d, 14d)
//
// let query_frequency = 14d;
// let query_period = 14d;
//let Function = (query_frequency:timespan = 14d, query_period:timespan = 14d){
let ipv4_prefix_mask = 23;
let session_period_limit = 7d;
let group_by_time_period = 30m;
let excluded_success_resulttypes = dynamic(["700082"]);
let _ExpectedIPAddresses = toscalar(
    union _GetWatchlist("IP-CorporateCollaborators"), _GetWatchlist("IP-Vendors")
    | summarize make_list(IPAddress)
);
let _ExpectedLocations = toscalar(
    _GetWatchlist("Activity-ExpectedSignificantActivity")
    | where Activity == "CorporateGeolocation"
    | summarize make_list(Auxiliar)
);
let _ExpectedASNs = toscalar(
    _GetWatchlist("Activity-ExpectedSignificantActivity")
    | where Activity == "CommonUserASN"
    | summarize make_list(Auxiliar)
);
let _HomeTenantId = toscalar(
    _GetWatchlist("UUID-AADTenantIds")
    | where Notes has "[HomeTenant]"
    | summarize make_list(TenantId)
);
let _PartialSuccessResultTypes = toscalar(
    _GetWatchlist("ResultType-SignInLogsErrorCodes")
    | where Notes has_any ("[Success]", "[Expired]") and not(ResultType in (excluded_success_resulttypes))
    | summarize make_list(ResultType)
);
// Query authentication events
let _SigninEvents =
    union isfuzzy=true SigninLogs, AADNonInteractiveUserSignInLogs, ADFSSignInLogs
    | where TimeGenerated > ago(query_period)
    | where ResourceTenantId in (_HomeTenantId)
    | where not(IPAddress == "127.0.0.1" or ipv4_is_private(IPAddress))
    // Remove expected IP addresses
    | where not(ipv4_is_in_any_range(IPAddress, _ExpectedIPAddresses))
    // Distinguish failed and (partially) successful authentications
    | extend PartialSuccessResultType = ResultType in (_PartialSuccessResultTypes)
;
let _GetSprayStages = (start_time:datetime, end_time:datetime){
    // Query events where distinct accounts were observed from the same address range
    let _SprayEvents = materialize(
        _SigninEvents
        | where TimeGenerated between(start_time .. end_time)
        // Compute the first time an account had activity from an IP address
        | summarize hint.strategy=shuffle
            StartTime = minif(TimeGenerated, IsInteractive),
            take_any(Type)
            by UserId, IPAddress, PartialSuccessResultType
        // Group the first times by address range
        | summarize
            minTimeGeneratedList = make_list_if(tostring(pack_array(StartTime, UserId)), isnotempty(StartTime)),
            IPAddresses = make_set(IPAddress, tolong(min_of(pow(2, (32 - ipv4_prefix_mask)), 10000))),
            DistinctAccountCount = dcount(UserId)
            by IPRange = parse_ipv6_mask(IPAddress, 128 - (32 - ipv4_prefix_mask)), PartialSuccessResultType
        // Remove ranges with failures and less than 2 accounts or 2 events
        | where not(not(PartialSuccessResultType) and (DistinctAccountCount < 2 or array_length(minTimeGeneratedList) < 2))
        // Compute time periods between different accounts
        | mv-apply minTimeGeneratedElement = minTimeGeneratedList on (
            extend minTimeGeneratedElement = todynamic(dynamic_to_json(minTimeGeneratedElement))
            | extend
                TimeGenerated = todatetime(minTimeGeneratedElement[0]),
                UserId = tostring(minTimeGeneratedElement[1])
            | sort by TimeGenerated asc
            // Remove cases where the previous event was the same account (assumedly from another IP address)
            | where not(isnotempty(prev(UserId)) and UserId == prev(UserId))
            // Compute how much time passed until the next event
            | extend NextEventTimeDiff = next(TimeGenerated) - TimeGenerated
            | project-away minTimeGeneratedElement, TimeGenerated, UserId
            // Keep events where the next event happened within x time
            | where isnotempty(NextEventTimeDiff) and NextEventTimeDiff between (time(0s)..session_period_limit)
            // Count how many events happened with a specific frequency (e.g. from 0 to 30m, 30m to 1h, 1h to 1h30m)
            | summarize
                AdditionalEvents = count(),
                take_any(PartialSuccessResultType)
                by Frequency = bin(NextEventTimeDiff, group_by_time_period)
            // Remove frequencies of successful authentications that have lesser activity that 2 additional events per 5 hours
            // (thus if long periods between events, take only cases with many events)
            | where not(PartialSuccessResultType and AdditionalEvents < 2*(Frequency / 5h))
            | summarize
                SprayCount = 1 + sum(AdditionalEvents),
                SprayFrequencies = make_bag(pack(tostring(Frequency), AdditionalEvents)),
                FrequenciesList = make_list(AdditionalEvents)
        )
        | project-away minTimeGeneratedList
        // Compute the spray consistency based on Shannon entropy
        | extend FrequenciesList = array_concat(FrequenciesList, pack_array(1)) // Add noise
        | mv-apply Frequency = FrequenciesList to typeof(int) on (
            extend AuxProb = Frequency / toreal(SprayCount)
            | summarize SprayConsistency = -sum(AuxProb*log2(AuxProb))
        )
        | project-away FrequenciesList
        // Underestimate spray consistency of IP ranges with less than 5 events
        | extend SprayConsistency = SprayConsistency + iff(SprayCount <= 5, (5-SprayCount) / toreal(5), 0.0)
        // Fix spray values where there wasn't spray activity
        | extend
            SprayCount = iff(SprayCount == 1 and array_length(bag_keys(SprayFrequencies)) == 0, int(null), SprayCount),
            SprayConsistency = iff(SprayCount == 1 and array_length(bag_keys(SprayFrequencies)) == 0, real(null), SprayConsistency)
        // Remove ranges with failures but without spray activity
        | where not(not(PartialSuccessResultType) and isempty(SprayCount))
        // Pack the useful information
        | extend Properties = pack(
            "PartialSuccessResultType", PartialSuccessResultType,
            "DistinctAccountCount", DistinctAccountCount,
            "DistinctAddressCount", array_length(IPAddresses),
            "SprayCount", SprayCount,
            "SprayConsistency", SprayConsistency,
            "SprayFrequencies", SprayFrequencies,
            "IPAddresses", IPAddresses
            )
        | project PartialSuccessResultType, IPRange, IPAddresses, Properties
    );
    // Join together failed and successful activity
    let _JoinedIPRanges =
        _SprayEvents
        | where PartialSuccessResultType
        | project-rename Success_IPAddresses = IPAddresses
        | lookup kind=inner (
            _SprayEvents
            | where not(PartialSuccessResultType)
            | project-rename Failure_IPAddresses = IPAddresses
        ) on IPRange
        // Join ONLY if they share the same addresses set
        | where not(array_length(set_intersect(Success_IPAddresses, Failure_IPAddresses)) == 0)
        | project IPRange, Joined = true
    ;
    _SprayEvents
    | lookup kind=leftouter _JoinedIPRanges on IPRange
    | extend Joined = iff(isempty(Joined), PartialSuccessResultType, Joined)
    | summarize
        Activity = make_bag(pack(iff(PartialSuccessResultType, "SuccessEvents", "FailureEvents"), Properties)),
        IPAddresses = make_set(IPAddresses, tolong(min_of(pow(2, (32 - ipv4_prefix_mask)), 10000)))
        by IPRange, Joined
    | project-away Joined
    // Tag the potential stage of an attack
    | extend Stage = case(
        isnotempty(Activity["FailureEvents"]) and isnotempty(Activity["SuccessEvents"]), "Password Spray + Initial Access",
        isnotempty(Activity["FailureEvents"]) and isempty(Activity["SuccessEvents"]), "Password Spray",
        isempty(Activity["FailureEvents"]) and isnotempty(Activity["SuccessEvents"]), "Initial Access",
        ""
        )
    // Remove ranges without failures nor spray activity
    | where not(
        Stage == "Initial Access"
        and isempty(Activity["SuccessEvents"]["SprayCount"])
        )
    // Remove ranges, with failures and successes, with low activity
    | where not(
        Stage == "Password Spray + Initial Access"
        and toint(Activity["SuccessEvents"]["SprayCount"]) < 3
        and toint(Activity["FailureEvents"]["SprayCount"]) < 3
        )
    // Remove ranges where the failures don't cover extensively the successes
    | extend Auxiliar_FailureOverSuccessCoverage =
        100 *
        (toreal(Activity["FailureEvents"]["SprayCount"]) / toreal(Activity["SuccessEvents"]["DistinctAccountCount"])) *
        (toreal(Activity["FailureEvents"]["SprayCount"]) / toreal(Activity["FailureEvents"]["DistinctAccountCount"])) *
        (toreal(Activity["FailureEvents"]["DistinctAddressCount"]) / toreal(Activity["SuccessEvents"]["DistinctAddressCount"])) *
        iff(isnotempty(Activity["SuccessEvents"]["SprayConsistency"]), (toreal(Activity["SuccessEvents"]["SprayConsistency"]) / toreal(Activity["FailureEvents"]["SprayConsistency"])), 1.0)
    | where not(
        Stage == "Password Spray + Initial Access"
        and isnotempty(Auxiliar_FailureOverSuccessCoverage) and Auxiliar_FailureOverSuccessCoverage < 1
        )
    | project-away Auxiliar_FailureOverSuccessCoverage
};
// Get the events relevant to query_frequency
let _SprayStages = materialize(
    _GetSprayStages(ago(query_period), now())
    | lookup kind=leftouter (
        _GetSprayStages(ago(query_period), ago(query_frequency))
        | project Stage, IPRange, Recurrent = true
    ) on Stage, IPRange
    | extend Recurrent = iff(isempty(Recurrent), false, Recurrent)
);
// Get the list of IP addresses
let _SprayingAddresses = toscalar(
    _SprayStages
    | mv-expand IPAddress = IPAddresses to typeof(string)
    | summarize make_list(IPAddress)
);
// Add information about start time, location, ASNs, result types, devices...
_SprayStages
| lookup kind=leftouter (
    _SigninEvents
    | where IPAddress in (_SprayingAddresses)
    //| where not(isempty(DeviceDetail_string) and isempty(DeviceDetail_dynamic) and isempty(UserAgent))
    | project
        TimeGenerated,
        IPAddress,
        UserDisplayName,
        ResultType,
        Location,
        AutonomousSystemNumber,
        UserAgent,
        DeviceDetail = iff(isnotempty(DeviceDetail_string), DeviceDetail_string, tostring(DeviceDetail_dynamic))
    // Take 1 sample event per "source"
    | extend Auxiliar_GroupBy = strcat(IPAddress, UserAgent)
    | summarize hint.strategy=shuffle
        take_any(*),
        StartTime = min(TimeGenerated),
        EndTime = max(TimeGenerated),
        UserDisplayNames = make_set_if(UserDisplayName, not(UserDisplayName matches regex @"[a-f0-9]+\-[a-f0-9]+\-[a-f0-9]+\-[a-f0-9]+\-[a-f0-9]+"), 100)
        by Auxiliar_GroupBy
    | project-away Auxiliar_GroupBy
    | extend
        IPRange = parse_ipv6_mask(IPAddress, 128 - (32 - ipv4_prefix_mask)),
        DeviceDetail = todynamic(DeviceDetail),
        ParsedUserAgent = parse_user_agent(UserAgent, dynamic(["os", "browser"]))
    | extend
        DeviceId = tostring(DeviceDetail["deviceId"]),
        DeviceName = tostring(DeviceDetail["displayName"]),
        DeviceIsManaged = tostring(DeviceDetail["isManaged"]),
        DeviceTrustType = tostring(DeviceDetail["trustType"]),
        DeviceDetailOS = tostring(DeviceDetail["operatingSystem"]),
        UserAgentOS = tostring(ParsedUserAgent["OperatingSystem"]["Family"]),
        Browser = tostring(ParsedUserAgent["Browser"]["Family"])
    | extend
        OperatingSystem = case(
            isempty(DeviceDetailOS), UserAgentOS,
            isempty(UserAgent), extract(@"^([A-Za-z]+)", 1, DeviceDetailOS),
            UserAgentOS == "Other", extract(@"^([A-Za-z]+)", 1, DeviceDetailOS),
            UserAgentOS
        ),
        Browser = case(
            Browser == "Other", iff(UserAgent != "-", extract(@"^([^\/\s]+)", 1, UserAgent), ""),
            Browser
        )
    | extend
        OperatingSystem = case(
            OperatingSystem has "ios", "iOS",
            OperatingSystem has_any ("mac", "macos"), "macOS",
            OperatingSystem == "Ubuntu", "Linux",
            Browser == "Samsung Internet", "Android",
            Browser == "MacOutlook", "macOS",
            OperatingSystem
        )
    | project IPRange, StartTime, EndTime, UserDisplayNames, ResultType, Location, AutonomousSystemNumber, DeviceId, DeviceName, DeviceIsManaged, DeviceTrustType, Browser, OperatingSystem
    | partition hint.strategy=shuffle by IPRange (
        summarize
            StartTime = min(StartTime),
            EndTime = max(EndTime),
            UserDisplayNames = make_set(UserDisplayNames, 100),
            ResultTypes = make_set(ResultType, 100),
            Locations = make_set_if(Location, isnotempty(Location), 100),
            AutonomousSystemNumbers = make_set_if(AutonomousSystemNumber, isnotempty(AutonomousSystemNumber), 100),
            //DeviceIds = make_set_if(DeviceId, isnotempty(DeviceId), 100),
            DeviceNames = make_set_if(DeviceName, isnotempty(DeviceName), 100),
            DeviceIsManaged = make_set_if(DeviceIsManaged, isnotempty(DeviceIsManaged), 100),
            DeviceTrustTypes = make_set_if(DeviceTrustType, isnotempty(DeviceTrustType), 100),
            Browsers = make_set_if(Browser, isnotempty(Browser), 100),
            OSCount = count(),
            take_any(IPRange)
            by OperatingSystem
        | summarize
            StartTime = min(StartTime),
            EndTime = max(EndTime),
            UserDisplayNamesSample = array_sort_asc(make_set(UserDisplayNames, 100)),
            ResultTypesSample = array_sort_asc(make_set(ResultTypes, 100)),
            Locations = array_sort_asc(make_set(Locations, 100)),
            AutonomousSystemNumbers = array_sort_asc(make_set(AutonomousSystemNumbers, 100)),
            //DeviceIds = array_sort_asc(make_set(DeviceIds, 100)),
            DeviceNamesSample = array_sort_asc(make_set(DeviceNames, 100)),
            DeviceIsManagedSample =  array_sort_asc(make_set(DeviceIsManaged, 100)),
            DeviceTrustTypesSample = array_sort_asc(make_set(DeviceTrustTypes, 100)),
            Browsers = array_sort_asc(make_set(Browsers, 100)),
            OperatingSystems = make_bag(pack(OperatingSystem, OSCount)),
            OperatingSystemsCount = sum(OSCount),
            take_any(IPRange)
    )
    | extend
        Location = case(
            array_length(Locations) == 1, tostring(Locations[0]),
            ""
        ),
        ASN = case(
            array_length(AutonomousSystemNumbers) == 1, tostring(AutonomousSystemNumbers[0]),
            ""
        )
) on IPRange
// Remove ranges without failures that might be mobile ISP ranges
| where not(
    Stage == "Initial Access"
    and Location in (_ExpectedLocations)
    and ASN in (_ExpectedASNs)
    and (toint(Activity["SuccessEvents"]["DistinctAddressCount"]) > 10 or toint(Activity["SuccessEvents"]["DistinctAccountCount"]) > 10)
    and (toint(Activity["SuccessEvents"]["DistinctAddressCount"]) / toreal(Activity["SuccessEvents"]["DistinctAccountCount"])) >= 0.95
    and (toint(OperatingSystems["iOS"]) + toint(OperatingSystems["Android"])) >= 0.95*OperatingSystemsCount
    )
// Remove ranges without failures that might be common user ISP ranges
| where not(
    Stage == "Initial Access"
    and Location in (_ExpectedLocations)
    and ASN in (_ExpectedASNs)
    and (toint(Activity["SuccessEvents"]["DistinctAccountCount"]) - toint(Activity["SuccessEvents"]["DistinctAddressCount"])) <= 2
    and (toint(Activity["SuccessEvents"]["SprayCount"]) - toint(Activity["SuccessEvents"]["DistinctAddressCount"])) <= 2
    and (toint(OperatingSystems["iOS"]) >= 2 or toint(OperatingSystems["Android"]) >= 2)
    )
| where not(
    Stage == "Initial Access"
    and Location in (_ExpectedLocations)
    and ASN in (_ExpectedASNs)
    and (toint(Activity["SuccessEvents"]["SprayCount"]) <= 2
        or (toint(Activity["SuccessEvents"]["SprayCount"]) / toreal(Activity["SuccessEvents"]["SprayConsistency"]) / toreal(Activity["SuccessEvents"]["DistinctAccountCount"])) < 0.2)
    and (toint(OperatingSystems["iOS"]) >= 2 or toint(OperatingSystems["Android"]) >= 2)
    )
// Remove ranges without failures from expected locations with few accounts per address
| where not(
    Stage == "Initial Access"
    and Location in (_ExpectedLocations)
    and (toint(Activity["SuccessEvents"]["DistinctAccountCount"]) - toint(Activity["SuccessEvents"]["DistinctAddressCount"])) <= 1
    and (toint(Activity["SuccessEvents"]["SprayCount"]) - toint(Activity["SuccessEvents"]["DistinctAddressCount"])) <= 1
    )
| mv-apply UserDisplayName = UserDisplayNamesSample to typeof(string) on (
    reduce by UserDisplayName with characters=" "
    | top 1 by Count desc
    | extend UserDisplayNamesSample = case(
        Pattern != "others", pack("UserDisplayNamePattern", Pattern, "Count", Count),
        dynamic(null)
        )
    | project-away Pattern, Count, Representative
    )
// Optional tags based on the addresses
// | extend SuspiciousAddresses = _SuspiciousAddresses
// | mv-apply SuspiciousIP = SuspiciousAddresses to typeof(string) on (
//     extend Match = ipv6_compare(SuspiciousIP, IPRange,  128 - (32 - ipv4_prefix_mask))
//     | summarize Match = make_set(Match)
//     | extend MaliciousRange = Match has "0"
// )
// | project-away Match, SuspiciousAddresses
// | extend KnownAddresses = _ExpectedIPAddresses
// | mv-apply KnownIP = KnownAddresses to typeof(string) on (
//     extend Match = ipv6_compare(KnownIP, IPRange,  128 - (32 - ipv4_prefix_mask))
//     | summarize Match = make_set(Match)
//     | extend KnownRange = Match has "0"
// )
// | project-away Match, KnownAddresses
// Unpack useful information
// | extend FailureActivity = Activity["FailureEvents"]
// | evaluate bag_unpack(FailureActivity, OutputColumnPrefix="Failure_", ignoredProperties=dynamic(["PartialSuccessResultType", "IPAddresses"]))
// | extend SuccessActivity = Activity["SuccessEvents"]
// | evaluate bag_unpack(SuccessActivity, OutputColumnPrefix="Success_", ignoredProperties=dynamic(["PartialSuccessResultType", "IPAddresses"]))
// Format address range to IPv4 if possible
| extend HexCodes = split(extract(@"^(?i:0+\:0+\:0+\:0+\:0+\:ffff\:([a-f0-9]+\:[a-f0-9]+))$", 1, IPRange), ":")
| extend
    IPRange = case(
        array_length(HexCodes) == 2, format_ipv4_mask(tolong(strcat("0x", tostring(HexCodes[0])))*65536 + tolong(strcat("0x", tostring(HexCodes[1]))), ipv4_prefix_mask),
        IPRange
    ),
    AddressScope = case(
        array_length(HexCodes) == 2, ipv4_prefix_mask,
        128 - (32 - ipv4_prefix_mask)
    )
| extend Address = tostring(split(IPRange, "/", 0))
| mv-apply with_itemindex = Index_aux IPAddress = IPAddresses to typeof(string) on (
    extend Entities = pack("$id", tostring(Index_aux + 3), "Address", tostring(IPAddress), "Type", "ip")
    | summarize Entities = make_list(Entities)
    )
| extend Entities = tostring(array_sort_asc(array_concat(pack_array(pack("$id", tostring(2), "Address", todynamic(Address)[0], "AddressScope", tostring(AddressScope), "Type", "ip")), Entities)))
| extend
    Entities = case(
        Stage == "Password Spray", Entities,
        Stage == "Password Spray + Initial Access", "",
        Stage == "Initial Access", "",
        Entities
    ),
    AlertName = case(
        Stage == "Password Spray" and Recurrent, "Slow password spray attack - Recurrent address range",
        Stage == "Password Spray" and not(Recurrent), "Slow password spray attack",
        Stage == "Password Spray + Initial Access", "Slow password spray attack - Potential compromised account",
        Stage == "Initial Access", "Authentication of several accounts from unexpected source",
        "Slow password spray attack"
    ),
    AlertSeverity = case(
        Stage == "Password Spray", "Informational",
        Stage == "Password Spray + Initial Access" and Location in (_ExpectedLocations), "Medium",
        Stage == "Initial Access" and Location in (_ExpectedLocations), "Low",
        "High"
    ),
    BenignAlert = case(
        Stage == "Initial Access" and Recurrent, true,
        Stage == "Password Spray + Initial Access" and Recurrent, true,
        false
    )
| where not(BenignAlert)
| sort by Stage desc, toint(ASN) asc, toint(extract(@"^(\d+)", 1, IPRange)) asc, toint(extract(@"^\d+\.(\d+)", 1, IPRange)) asc, toint(extract(@"^\d+\.\d+\.(\d+)", 1, IPRange)) asc, toint(extract(@"^\d+\.\d+\.\d+\.(\d+)", 1, IPRange)) asc
| project-away HexCodes, Address, AddressScope, OperatingSystemsCount, Locations, AutonomousSystemNumbers
| project-reorder
    Stage,
    StartTime,
    EndTime,
    MaliciousRange*,
    KnownRange*,
    ResultTypesSample,
    Location,
    ASN,
    IPRange,
    IPAddresses,
    Success_*,
    Failure_*,
    OperatingSystems,
    Browsers,
    //DeviceIds,
    DeviceNamesSample,
    DeviceIsManagedSample,
    DeviceTrustTypesSample,
    UserDisplayNamesSample,
    Activity,
    AlertName,
    AlertSeverity,
    Entities
//};
//Function(query_frequency, query_period)
