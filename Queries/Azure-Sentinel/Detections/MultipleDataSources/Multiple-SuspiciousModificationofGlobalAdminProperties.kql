let query_frequency = 1h;
let query_period = 14d;
let _ExpectedEvents =
    _GetWatchlist("Activity-ExpectedSignificantActivity")
    | where Activity == "GlobalAdminUpdate"
    | project InitiatorId = tostring(ActorId), OperationName = Auxiliar
;
IdentityInfo
| where TimeGenerated > ago(query_period)
| where set_has_element(AssignedRoles, "Global Administrator")
| distinct AccountUPN, AccountObjectId
| extend AccountUPN = tolower(AccountUPN)
| join kind=inner (
    AuditLogs
    | where TimeGenerated > ago(query_frequency)
    | where Category == "UserManagement" and AADOperationType == "Update" // and Result =~ "success"
    // | where isnotempty(InitiatedBy["user"])
    | mv-expand TargetResource = TargetResources
    | where TargetResource["type"] == "User"
    | extend AccountObjectId = tostring(TargetResource["id"])
    | where not(LoggedByService == "Core Directory" and OperationName has "update" and tostring(TargetResource["modifiedProperties"]) == "[]")
    | mv-apply modifiedProperty = TargetResource["modifiedProperties"] on (
        summarize modifiedProperties = make_bag(
            bag_pack(tostring(modifiedProperty["displayName"]),
                bag_pack("oldValue", trim(@'[\"\s]+', tostring(modifiedProperty["oldValue"])),
                    "newValue", trim(@'[\"\s]+', tostring(modifiedProperty["newValue"])))))
    )
    | where not(LoggedByService == "Core Directory" and OperationName has "update" and tostring(modifiedProperties["Action Client Name"]["newValue"]) == "DirectorySync")
    | where not(LoggedByService == "Core Directory" and OperationName has "Update user" and isnotempty(modifiedProperties["Included Updated Properties"]) and tostring(modifiedProperties["Included Updated Properties"]["newValue"]) in ("LastDirSyncTime", ""))
    | where not(LoggedByService == "Core Directory" and OperationName == "Update user" and tostring(modifiedProperties["Included Updated Properties"]["newValue"]) == "StrongAuthenticationPhoneAppDetail" and isnotempty(modifiedProperties["StrongAuthenticationPhoneAppDetail"]) and tostring(array_sort_asc(extract_all(@'\"Id\"\:\"([^\"]+)\"', tostring(modifiedProperties["StrongAuthenticationPhoneAppDetail"]["newValue"])))) == tostring(array_sort_asc(extract_all(@'\"Id\"\:\"([^\"]+)\"', tostring(modifiedProperties["StrongAuthenticationPhoneAppDetail"]["oldValue"])))))
    | extend
        Initiator = iif(isnotempty(InitiatedBy["app"]), tostring(InitiatedBy["app"]["displayName"]), tostring(InitiatedBy["user"]["userPrincipalName"])),
        InitiatorId = iif(isnotempty(InitiatedBy["app"]), tostring(InitiatedBy["app"]["servicePrincipalId"]), tostring(InitiatedBy["user"]["id"])),
        IPAddress = tostring(InitiatedBy[tostring(bag_keys(InitiatedBy)[0])]["ipAddress"])
    | join kind=leftanti _ExpectedEvents on InitiatorId, OperationName
    ) on AccountObjectId
| project
    TimeGenerated,
    LoggedByService,
    Category,
    Initiator,
    IPAddress,
    OperationName,
    Result,
    ResultDescription,
    AccountUPN,
    InitiatedBy,
    AdditionalDetails,
    TargetResources,
    AccountObjectId,
    InitiatorId,
    CorrelationId
