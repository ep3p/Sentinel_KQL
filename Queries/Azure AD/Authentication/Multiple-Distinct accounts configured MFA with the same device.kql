let query_frequency = 1h;
let query_period = 14d;
AuthenticationMethodsChanges()
| where TimeGenerated > ago(query_period)
| where TargetResources has_any ("PhoneNumber", "DeviceToken") or UpdateUser_TargetResources has_any ("PhoneNumber", "DeviceToken")
| extend
    PhoneNumbers = extract_all(@'PhoneNumber\"\:\"([^\"]+)', tostring(column_ifexists("StrongAuthenticationUserDetails", dynamic(null))["newValue"])),
    DeviceTokens = extract_all(@'DeviceToken\"\:\"([^\"]+)', tostring(column_ifexists("StrongAuthenticationPhoneAppDetail", dynamic(null)).newValue))
| mv-apply ModifiedProperties = TargetResources[0]["modifiedProperties"] on (
    summarize AuxiliarPhoneNumbers = make_set_if(
        trim(@'\"', tostring(ModifiedProperties["newValue"])),
        tostring(ModifiedProperties["displayName"]) endswith "PhoneNumber" and isnotempty(trim(@'\"', tostring(ModifiedProperties["newValue"]))))
        // Phone.Phone.PhoneNumber TemporaryAccessPass.Phone.PhoneNumber
    )
| extend PhoneNumbers = set_union(PhoneNumbers, AuxiliarPhoneNumbers)
| mv-expand RepeatedDevice = array_concat(PhoneNumbers, DeviceTokens) to typeof(string)
| extend RepeatedDevice = iff(RepeatedDevice == "NO_DEVICE_TOKEN", "", RepeatedDevice)
| as _Changes
| join kind=leftsemi (
    _Changes
    | where isnotempty(RepeatedDevice)
    | summarize
        EndTime = max(TimeGenerated),
        DistinctTargets = count_distinct(coalesce(TargetId, TargetUserPrincipalName))
        by RepeatedDevice
    | where EndTime > ago(query_frequency) and DistinctTargets > 1
    ) on RepeatedDevice
| sort by RepeatedDevice asc, TimeGenerated asc
| project
    TimeGenerated,
    LoggedByService,
    Category,
    AADOperationType,
    Initiator,
    IPAddress,
    OperationName,
    Result,
    ResultDescription,
    TargetUserPrincipalName,
    TargetId,
    RepeatedDevice,
    PhoneNumbers,
    DeviceTokens,
    StrongAuthenticationUserDetails = column_ifexists("StrongAuthenticationUserDetails", dynamic(null)),
    StrongAuthenticationPhoneAppDetail = column_ifexists("StrongAuthenticationPhoneAppDetail", dynamic(null)),
    AdditionalDetails,
    InitiatorId,
    InitiatedBy,
    TargetResources,
    CorrelationId,
    UpdateUser_TargetResources,
    UpdateUser_CorrelationId
