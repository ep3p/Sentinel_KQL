let query_frequency = 30m;
let query_period = 1h;
let account_threshold = 5;
AADNonInteractiveUserSignInLogs
//| where ResultType == "81016"
| where ResultType startswith "81"
| as _Events
| join kind=leftsemi (
    _Events
    | evaluate activity_counts_metrics(UserId, TimeGenerated, ago(query_period), now(), query_frequency, ResultType)
    | summarize
        PreviousTimeGenerated = arg_min(TimeGenerated, PreviousUserCount = ["new_dcount"]),
        CurrentTimeGenerated = arg_max(TimeGenerated, CurrentUserCount = ["new_dcount"])
        by ResultType
    | where CurrentUserCount > account_threshold
        or (not(PreviousUserCount > account_threshold) and (PreviousUserCount + CurrentUserCount) > account_threshold)
    ) on ResultType
