let query_frequency = 1h;
let query_period = 2d;
let ingestion_period = 1d;
union
    (
    SubscriptionInventoryLogs
    | where TimeGenerated > ago(query_frequency)
    | summarize arg_max(TimeGenerated, *) by SubscriptionId
    ),
    (
    SubscriptionInventoryLogs
    | where TimeGenerated between (ago(query_period) .. ago(query_frequency))
    | summarize arg_max(TimeGenerated, *) by SubscriptionId
    )
| summarize
    minTimeGenerated = arg_min(TimeGenerated, State),
    maxTimeGenerated = arg_max(TimeGenerated, *)
    by SubscriptionId
| project-rename PreviousState = State, CurrentState = State1
| extend AlertName = case(
    minTimeGenerated > ago(query_frequency), strcat("Azure subscription created", " - ", ResourceName),
    maxTimeGenerated between (ago(query_frequency + ingestion_period) .. ago(ingestion_period)), strcat("Azure subscription removed", " - ", ResourceName),
    isnotempty(PreviousState) and isnotempty(CurrentState) and CurrentState != PreviousState, strcat("Azure subscription ", tolower(CurrentState), " - ", ResourceName),
    ""
    )
| where isnotempty(AlertName)
| project
    TimeGenerated = maxTimeGenerated,
    ResourceName,
    SubscriptionId,
    PreviousState,
    CurrentState,
    ResourceId,
    AlertName
