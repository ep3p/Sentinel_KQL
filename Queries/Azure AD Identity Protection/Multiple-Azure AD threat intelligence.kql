let query_frequency = 5m;
let query_period = 2d;
AADUserRiskEvents
| where TimeGenerated > ago(query_period)
| where OperationName == "User Risk Detection" and Source == "IdentityProtection" and RiskEventType == "investigationsThreatIntelligence"
| summarize minTimeGenerated = min(TimeGenerated), arg_max(TimeGenerated, *) by Id
| where minTimeGenerated > ago(query_frequency)
| project
    //TimeGenerated,
    OperationName,
    Source,
    Activity,
    UserDisplayName,
    UserPrincipalName,
    UserId,
    CorrelationId,
    RiskEventType,
    RiskState,
    RiskDetail,
    RiskLevel
| as _Events
| lookup kind=leftouter (
    (SigninLogs
    | where TimeGenerated > ago(query_period)
    | where CorrelationId in (toscalar(_Events | summarize make_list(CorrelationId))) and RiskState != "none"// and RiskEventTypes_V2 has "azureADThreatIntel"
    | extend
        DeviceDetail = tostring(DeviceDetail),
        TimeReceived = _TimeReceived
    )
    | summarize
        arg_max(TimeReceived, *)
        by OriginalRequestId
    | project
        TimeGenerated,
        Type,
        // UserPrincipalName,
        // UserDisplayName,
        IPAddress,
        AutonomousSystemNumber,
        Location,
        ResultType,
        ResultDescription,
        ClientAppUsed,
        AppDisplayName,
        ResourceDisplayName,
        DeviceDetail,
        UserAgent,
        AuthenticationProtocol,
        AuthenticationDetails,
        ConditionalAccessStatus,
        ConditionalAccessPolicies,
        UserId,
        OriginalRequestId,
        CorrelationId
    ) on UserId, CorrelationId
| project
    TimeGenerated,
    OperationName,
    Source,
    Activity,
    UserDisplayName,
    UserPrincipalName,
    RiskEventType,
    RiskState,
    RiskDetail,
    RiskLevel,
    Type,
    IPAddress,
    AutonomousSystemNumber,
    Location,
    ResultType,
    ResultDescription,
    ClientAppUsed,
    AppDisplayName,
    ResourceDisplayName,
    DeviceDetail,
    UserAgent,
    AuthenticationProtocol,
    AuthenticationDetails,
    ConditionalAccessStatus,
    ConditionalAccessPolicies,
    UserId,
    OriginalRequestId,
    CorrelationId
