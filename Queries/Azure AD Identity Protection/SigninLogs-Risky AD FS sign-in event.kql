let _ADFSTokenIssuerNames = toscalar(
    _GetWatchlist("Activity-ExpectedSignificantActivity")
    | where Activity == "ADFSTokenIssuerName"
    | summarize make_list(Auxiliar)
);
let _ADFSResourceIdentities = toscalar(
    _GetWatchlist("Activity-ExpectedSignificantActivity")
    | where Activity == "ADFSTokenIssuerName"
    | summarize make_list(DestinationAddress)
);
let _ExpectedResultTypes = toscalar(
    _GetWatchlist("ResultType-SignInLogsErrorCodes")
    | where isnotempty(ResultDescription) and not(Notes has_any ("[Success]", "[Expired]"))
    | summarize make_list(ResultType)
);
SigninLogs
| where TokenIssuerName has_any (_ADFSTokenIssuerNames) and RiskLevelAggregated != "none" and RiskState == "atRisk"
    // and ResourceIdentity has_any (_ADFSResourceIdentities) and AppId == "NotApplicable" and ResourceDisplayName == "SSO"
| where not(ResultType in (_ExpectedResultTypes))
| project
    TimeGenerated,
    CreatedDateTime,
    UserDisplayName,
    UserPrincipalName,
    IPAddress,
    Location,
    ResultType,
    ResultDescription,
    ResourceDisplayName,
    ResourceIdentity,
    AutonomousSystemNumber,
    TokenIssuerName,
    UserAgent,
    AuthenticationDetails,
    AuthenticationProcessingDetails,
    NetworkLocationDetails,
    RiskDetail,
    RiskEventTypes,
    RiskEventTypes_V2,
    RiskLevelAggregated,
    RiskLevelDuringSignIn,
    RiskState,
    UserId,
    OriginalRequestId,
    CorrelationId
//| union (AADUserRiskEvents | where TokenIssuerType == "ADFederationServices")
